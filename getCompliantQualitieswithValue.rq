#+ endpoint: http://fuseki:3030/reflexion/query
#+ method: GET
#+ summary: Retrieves compliant qualities with values for MR scan data, based on compliance to ACR guidelines.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX refl: <http://reflexion/acr#>
PREFIX re: <http://www.w3.org/2000/10/swap/reason#>
PREFIX ref: <http://purl.org/vocab/relationship/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX neurolog-dolce: 
<http://neurolog.unice.fr/ontoneurolog/v3.0/ontoneurolog-extension-of-dolce.owl#>
PREFIX neurolog-data: <http://neurolog.unice.fr/ontoneurolog/v3.0/ontoneurolog-mr-dataset-acquisition.owl#> 

SELECT DISTINCT ?scan ?label ?quality_value
WHERE {
	?specquality rdfs:subClassOf+ refl:recommended-quality.  
  	FILTER NOT EXISTS {
    ?specquality rdfs:subClassOf+ [rdfs:subClassOf+ refl:recommended-quality].
  }
  ?property rdfs:subPropertyOf refl:acr-property.
  ?scan a refl:MR-scan-result; ?property ?quality.
  ?quality a ?specquality.
  ?specquality rdfs:label ?label.
  OPTIONAL {
    ?quality refl:acr-has-data-value ?value.
    BIND (str(?value) AS ?quality_value).
  }
  OPTIONAL {
    ?quality ?property2 [neurolog-dolce:has-for-name ?value].
    BIND (strafter(str(?value),"#") AS ?quality_value).
  }
}
