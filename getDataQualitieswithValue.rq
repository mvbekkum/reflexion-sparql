#+ endpoint: http://fuseki:3030/reflexion/query
#+ method: GET
#+ summary: Retrieves ACR guideline qualities present in the MR scan data with associated values.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX refl: <http://reflexion/acr#>
PREFIX re: <http://www.w3.org/2000/10/swap/reason#>
PREFIX ref: <http://purl.org/vocab/relationship/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX neurolog-dolce: 
<http://neurolog.unice.fr/ontoneurolog/v3.0/ontoneurolog-extension-of-dolce.owl#>
PREFIX neurolog-data: <http://neurolog.unice.fr/ontoneurolog/v3.0/ontoneurolog-mr-dataset-acquisition.owl#> 

SELECT DISTINCT ?scan ?quality_label ?quality_value
WHERE {
 
  ?acr_property rdfs:subPropertyOf refl:acr-property.
  ?acr_property rdfs:range ?range.
  
  ?scan a refl:MR-scan-result.
  ?scan ?acr_property ?quality.
  BIND (strafter(str(?range),"#") AS ?quality_label1). 
 
  OPTIONAL {
    ?quality refl:acr-has-data-value ?value1.
    BIND(str("") AS ?quality_label2).
    BIND (str(?value1) AS ?quality_value).
  }
  OPTIONAL {
    ?quality ?property2 [neurolog-dolce:has-for-name ?value2].
    ?property2 rdfs:range ?range2.
    BIND (CONCAT("/"^^xsd:string,strafter(str(?range2),"#")) AS ?quality_label2).
    BIND (strafter(str(?value2),"#") AS ?quality_value).
  }

  BIND(CONCAT(str(?quality_label1), str(?quality_label2)) AS ?quality_label).
}
